using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.IO;
using System.Linq;

using Tao;
using Tao.Metadata;
using Tao.Metadata.Tables;
using Tao.Metadata.Tables.Rows;
using Tao.Metadata.Signatures;
using Tests.Macros;

namespace Tests
{
    public class MemberRefRowTests : FileBasedTableTest
    {
        public ShouldBeAbleToExtractMethodDefSignature() : void
        {            
            // Read the heap data
            def heap = TablesHeap();
            def tableData = MemoryStream(base.GetTableStreamData());
            heap.ReadFrom(tableData);
            
            // Search for the AssemblyTrademark attribute constructor (should be in MemberRef, row 7)
            def table = heap.Tables.Where(t => t is MemberRefTable).First() :> MemberRefTable;
                        
            // Find the MemberRefRow that corresponds with the AssemblyTrademark constructor
            def root = GetMetadataRoot();
            def stringHeap = root.Heaps["#Strings"];            
            
            mutable targetRow : MemberRefRow = null;
            foreach(row in table.Rows)
            {
                def classRowRef = row.Class.ToRowRef(CodedTokenType.MemberRefParent);
                when(classRowRef.TableId == TableId.TypeRef)
                {
                    def typeRefRow = heap.GetRow.[TypeRefRow](classRowRef.Row);
                    def typeName = typeRefRow.TypeName.ToString(stringHeap);
                    when(typeName == "AssemblyTrademarkAttribute")
                    {
                        targetRow = row;
                    }
                }
            }
            
            targetRow.ShouldBeNotNull("Unable to find the AssemblyTrademark row in the MemberRef table");                        
                        
            def blobStream = root.Heaps["#Blob"];
            
            // The expected and actual blob signatures must match                        
            def actualSignature = targetRow.GetMethodSignature(blobStream) :> MethodRefSignature;
            actualSignature.ParameterCount.ShouldBeEqualTo(1, "Wrong Parameter Count");
            
            def parameters = actualSignature.Parameters;
            parameters[0].Type.ShouldBeEqualTo(StringSignature.Instance, "Wrong Parameter Type");
            
            // Match the method name            
            targetRow.Name.ToString(stringHeap).ShouldBeEqualTo(".ctor", "Wrong Method Name");
        }        
        protected override TargetAssemblyFileName : string 
        { 
            get
            {
                "SampleCustomAttributeUsage.dll";
            }
        }
    }
}
