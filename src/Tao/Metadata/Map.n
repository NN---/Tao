using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Collections.Concurrent;
using System.Linq;

using Tao;
using Tao.Interfaces;
namespace Tao.Metadata
{
    public class Map[TItem] : IWriter, IMap[TItem]
        where TItem : class
    {        
        private _map : IDictionary.[TItem, uint] = ConcurrentDictionary.[TItem, uint]();
        private _heap : MemoryStream = MemoryStream();
        private _writer : IWriter.[TItem];
        
        public this([NotNull] writer : IWriter.[TItem], map : IDictionary.[TItem, uint])
        {
            _writer = writer;
            _map = map;
        }
        public this([NotNull] writer : IWriter.[TItem], params items : array[TItem])
        {
            _writer = writer;
            foreach(item in items)
            {
                AddItem(item);
            }
        }

        public virtual Contains([NotNull] item : TItem) : bool
        {            
            _map.ContainsKey(item);
        }        
        public virtual GetOffset([NotNull] item : TItem) : uint
        {
            when(!Contains(item))
            {
                AddItem(item);
            }

            def result : uint = _map[item];

            result;
        }                
        
        public FlushContentsTo([NotNull] outputStream : Stream) : uint
            implements IWriter.Write
            requires outputStream.CanWrite
        {   
            def startPosition = 0;
            _heap.Seek(startPosition);
            outputStream.Seek(startPosition);

            def bytes = _heap.ToArray();
            def writer = BinaryWriter(outputStream);
            writer.Write(bytes);
            
            startPosition :> uint;
        }

        protected virtual AddItem([NotNull] item : TItem) : uint
        {
            def offset = _writer.Write(item, _heap);
            _map[item] = offset;
            
            offset;
        }
    }
}
