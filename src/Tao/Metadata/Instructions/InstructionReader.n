using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

using Tao.Metadata.Instructions.Operands;
namespace Tao.Metadata.Instructions
{
    // TODO: Refactor the UserStringHeap usage and replace it with the IStringHeapReader interface dependency
    public class InstructionReader : IInstructionReader
    {
        private _reader : IIndividualInstructionReader;        
        
        public this([NotNull] reader : IIndividualInstructionReader) 
        {
            _reader = reader;
        }
        
        public Read([NotNull] instructionStream : Stream) : array[IInstruction]
        {
            def instructionMap = Dictionary.[uint, IInstruction]();
            def codeSize = instructionStream.Length;
            mutable bytesRead : uint = 0;

            while(bytesRead < codeSize)
            {
                def startPosition = instructionStream.Position :> uint;
                _reader.ReadInstruction(instructionStream, instructionMap);

                def currentPosition = (instructionStream.Position :> uint);
                bytesRead += currentPosition - startPosition;                
            }

            def results = instructionMap.Values.ToArray();
            def keys = instructionMap.Keys.ToArray();
            
            // Resolve all the lazy instructions            
            for(mutable i = 0; i < results.Length; i++)
            {
                def currentInstruction = results[i];
                when(currentInstruction is LazyInstruction && currentInstruction != null)
                {
                    def lazyInstruction = currentInstruction :> LazyInstruction;
                    def realInstruction = lazyInstruction.Resolve();
                    
                    // Swap the lazy instruction with the actual instruction in the instruction map
                    def offset = keys[i];
                    instructionMap[offset] = realInstruction;
                    
                    results[i] = realInstruction;
                }
            }

            results;
        }            
    }
}
