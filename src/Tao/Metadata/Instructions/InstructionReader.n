using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

using Tao.Metadata.Instructions.Operands;
namespace Tao.Metadata.Instructions
{
    public class InstructionReader
    {
        private static operandReaderMap : OperandReaderMap = OperandReaderMap();
        
        public Read([NotNull] instructionStream : Stream, [NotNull] userStringHeap : Stream) : array[Instruction]
        {
            def instructionMap = Dictionary.[uint, Instruction]();
            def codeSize = instructionStream.Length;
            mutable bytesRead : uint = 0;

            while(bytesRead < codeSize)
            {
                def startPosition = instructionStream.Position :> uint;
                ReadInstruction(instructionStream, userStringHeap, instructionMap);

                def currentPosition = (instructionStream.Position :> uint);
                bytesRead += currentPosition - startPosition;                
            }

            instructionMap.Values.ToArray();
        }
        private ReadInstruction([NotNull] instructionStream : Stream, [NotNull] userStringHeap : Stream, instructionMap : Dictionary.[uint, Instruction]) : void
            // There should only be one instance of an instruction per offset 
            requires !instructionMap.ContainsKey(instructionStream.Position :> uint)
        {            

            def decoder = OpCodeDecoder();

            // Get the offset
            def offset = instructionStream.Position :> uint;

            // Read the OpCode
            def opCode = decoder.Decode(instructionStream);

            // Read the operand that corresponds with the currrent OpCode
            mutable operand = null;

            when(operandReaderMap.ContainsKey(opCode))
            {
                def operandReader = operandReaderMap[opCode];
                def rawOperandValue = operandReader.ReadOperandFrom(instructionStream);
                operand = Reinterpret(rawOperandValue, opCode, userStringHeap);
            }

            def instruction = Instruction(offset, opCode, operand);
            instructionMap[offset] = instruction;
        }
        
        private Reinterpret([NotNull] operandValue : object, opCode : OpCode, [NotNull] userStringHeap : Stream) : object
        {
            // Leave the operand value untouched by default
            mutable result = operandValue;
            
            // Convert the user string offsets stored in the operand into an actual string            
            when(opCode == OpCodes.Ldstr)
            {
                def offset = operandValue :> uint;
                def reader = UserStringHeapReader();
                result = reader.Read(offset, userStringHeap) : object;
            }
            
            result;
        }
    }
}
