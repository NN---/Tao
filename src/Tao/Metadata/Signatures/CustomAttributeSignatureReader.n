using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Text;
using System.Linq;

using Tao.Metadata.Tables;
using Tao.Metadata.Tables.Rows;

namespace Tao.Metadata.Signatures
{
    public class CustomAttributeSignatureReader
    {
        public Read([NotNull] parameters : IEnumerable.[Param],[NotNull] blobStream : Stream, [NotNull] stringHeap : Stream, [NotNull] tablesHeap : ITablesHeap) : CustomAttributeSignature
        {
            def reader = BinaryReader(blobStream);          
            def prolog = reader.ReadUInt16();
            when(prolog != 0x0001)
            {
                throw CustomAttributeSignaturePrologNotFoundException("Invalid custom attribute signature--the custom attribute signature is missing the prolog bytes!");
            }

            def signature = CustomAttributeSignature();
            def currentParameters = parameters.ToArray();
            foreach(param in currentParameters)
            {
                def value = ReadParameterValue(param, reader, stringHeap, blobStream, tablesHeap);
                def fixedArgument = CustomAttributeArgument(value);

                signature.FixedArguments.Add(fixedArgument);              
            }

            // TODO: Read the NumNamed value
            // TODO: Read the NamedArgs

            signature;
        }
        private ReadParameterValue([NotNull] param : Param, [NotNull] reader : BinaryReader, [NotNull] stringHeap : Stream, [NotNull] blobStream : Stream, [NotNull] tablesHeap : ITablesHeap) : object
            requires param.Type != null
        {
            def paramType = param.Type;
            def elementType = paramType.ElementType;

            def readerMap : Dictionary.[ElementType, void -> object] = Dictionary();
            readerMap[ElementType.I1] = reader.ReadSByte;
            readerMap[ElementType.I2] = reader.ReadInt16;
            readerMap[ElementType.I4] = reader.ReadInt32;
            readerMap[ElementType.I8] = reader.ReadInt64;
            readerMap[ElementType.U1] = reader.ReadByte;
            readerMap[ElementType.U2] = reader.ReadUInt16;
            readerMap[ElementType.U4] = reader.ReadUInt32;
            readerMap[ElementType.U8] = reader.ReadUInt64;
            readerMap[ElementType.Boolean] = reader.ReadBoolean;
            readerMap[ElementType.Char] = reader.ReadChar;
            readerMap[ElementType.R4] = reader.ReadSingle;    
            readerMap[ElementType.R8] = reader.ReadDouble;
            readerMap[ElementType.String] = () => ReadSerString(reader);
            readerMap[ElementType.Object] = () => ReadBoxedObject(reader, readerMap);
            readerMap[ElementType.ValueType] = () => ReadValueType(reader, paramType, stringHeap, blobStream, tablesHeap);

            when(!IsSystemType(param, stringHeap, tablesHeap) && !readerMap.ContainsKey(elementType))
            {
                throw NotSupportedException("Element Type Not Supported: " + elementType.ToString());
            }          

            if(elementType == ElementType.Class)
            {
                def typeRef = TypeReference();
                typeRef.QualifiedName = ReadSerString(reader);

                typeRef;
            }
            else
            {
                readerMap[elementType]();
            }
        }        

        private IsSystemType([NotNull] param : Param,[NotNull] stringHeap : Stream, [NotNull] tablesHeap : ITablesHeap) : bool      
            requires param.Type != null
        {
            mutable result : bool = false;
            def type = param.Type;
            when(type.GetType() == typeof(TypeDefOrRefEncodedSignature))
            {
                def signature = type :> TypeDefOrRefEncodedSignature;
                result = signature.IsSystemType(stringHeap, tablesHeap);
            }

            result;
        }

        private ReadValueType([NotNull] reader : BinaryReader, [NotNull] paramType : ITypeSignature, [NotNull] stringHeap : Stream, [NotNull] blobHeap : Stream, [NotNull] tablesHeap : ITablesHeap) : object
        {
            def parameterType = paramType :> ValueTypeSignature;
            def signature = TypeDefOrRefEncodedSignature();
            
            signature.Row = parameterType.Row;
            signature.TableId = parameterType.TableId;
            
            def typeRow = signature.GetTypeRow(tablesHeap);
            def typeName = typeRow.TypeName.ToString(stringHeap);
            def nameSpace = typeRow.TypeNamespace.ToString(stringHeap);
            
            def assemblyRow = typeRow.GetAssemblyRow(tablesHeap);            
            def assemblyName = assemblyRow.Name.ToString(stringHeap);                                                 
            
            def format = "{0}.{1}, {2}";
            def fullyQualifiedName = string.Format(format, nameSpace, typeName, assemblyName);
            
            def enumType = Type.GetType(fullyQualifiedName, true);
            def underlyingType = Enum.GetUnderlyingType(enumType);
            
            def readerMap = Dictionary.[Type, void -> object]();
            
            readerMap[typeof(byte)] = reader.ReadByte;
            readerMap[typeof(ushort)] = reader.ReadUInt16;
            readerMap[typeof(uint)] = reader.ReadUInt32;
            readerMap[typeof(ulong)] = reader.ReadUInt64;
            
            when(!readerMap.ContainsKey(underlyingType))
            {
                 throw NotSupportedException(string.Format("Underlying type '{0}' not supported when reading enums in custom attribute signatures", underlyingType.Name));
            }
            
            def enumValue = readerMap[underlyingType]();
            Enum.ToObject(enumType, enumValue);
        }
        
        private ReadBoxedObject([NotNull] reader : BinaryReader, [NotNull] readerMap : Dictionary.[ElementType, void -> object]) : object
        {
            def validElements = array[
                                    ElementType.Boolean,
                                    ElementType.Char,
                                    ElementType.I1,
                                    ElementType.I2,
                                    ElementType.I4,
                                    ElementType.I8,
                                    ElementType.U1,
                                    ElementType.U2,
                                    ElementType.U4,
                                    ElementType.U8,
                                    ElementType.R4,
                                    ElementType.R8,
                                    ElementType.String
                                    ];

            // Read the element type
            def elementByte : byte = reader.ReadByte();
            when(elementByte == 0x55)
            {
                // Handle custom enums here
            }

            when(elementByte == 0x1D)
            {
                // Handle single dimensional arrays here
            }

            def elementType = elementByte :> ElementType;
            when(!validElements.Contains(elementType))
            {
                throw BadImageFormatException(string.Format("Element type '{0}' is not a supported boxed element value for custom attribute signatures", elementType));
            }

            readerMap[elementType]();
        }
        private ReadSerString([NotNull] reader : BinaryReader) : string
        {
            def underlyingStream = reader.BaseStream;
            def lengthByteValue = underlyingStream.PeekByte();

            mutable result : string = null;
            match(lengthByteValue)
            {
                | 0x00 => result = string.Empty;
                | 0xFF => result = null;
                |_ => 
                    {
                        def integerReader = CompressedIntegerReader();
                        def length = integerReader.ReadInteger(underlyingStream);

                        def bytes = reader.ReadBytes(length :> int);
                        result = Encoding.UTF8.GetString(bytes);
                    }
            }

            result;
        }
    }
}
