using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Text;
using System.Linq;

using Tao.Metadata.Tables;
using Tao.Metadata.Tables.Rows;

namespace Tao.Metadata.Signatures
{
    public class CustomAttributeSignatureReader
    {
        public Read([NotNull] parameters : IEnumerable.[Param],[NotNull] blobStream : Stream, [NotNull] stringHeap : Stream, [NotNull] tablesHeap : ITablesHeap) : CustomAttributeSignature
        {
            def reader = BinaryReader(blobStream);          
            def prolog = reader.ReadUInt16();
            when(prolog != 0x0001)
            {
                throw CustomAttributeSignaturePrologNotFoundException("Invalid custom attribute signature--the custom attribute signature is missing the prolog bytes!");
            }

            def createMap = CreateReaderMap(reader, _, stringHeap, blobStream, tablesHeap);
            def signature = CustomAttributeSignature();
            def currentParameters = parameters.ToArray();
            foreach(param in currentParameters)
            {
                def paramType = param.Type;
                def elementType = paramType.ElementType;
                
                def readerMap = createMap(paramType);
                when(!param.IsSystemType(stringHeap, tablesHeap) && !readerMap.ContainsKey(elementType))
                {
                    throw NotSupportedException("Element Type Not Supported: " + elementType.ToString());
                }          

                def value = ReadParameterValue(elementType, reader, readerMap);

                def fixedArgument = CustomAttributeArgument(value);

                signature.FixedArguments.Add(fixedArgument);              
            }

            // Read the NumNamed value
            def numNamed : ushort = reader.ReadUInt16();

            def FIELD : byte = 0x53;
            def PROPERTY : byte = 0x54;

            def validTypes = array[
                ElementType.I1,
                ElementType.I2,
                ElementType.I4,
                ElementType.I8,
                ElementType.U1,
                ElementType.U2,
                ElementType.U4,
                ElementType.U8,
                ElementType.R4,
                ElementType.R8,
                ElementType.String
            ];
            
            // Read the NamedArgs            
            for(mutable i = 0; i < numNamed; i++)
            {
                def fieldOrPropByte = reader.ReadByte();
                when(fieldOrPropByte != FIELD && fieldOrPropByte != PROPERTY)
                {
                    throw BadImageFormatException("Invalid named argument in custom attribute signature. Expected either a FIELD(0x53) or a PROPERTY byte (0x54)");
                }

                // Read the field or prop type
                def elementType = reader.ReadByte() :> ElementType;
                when(!validTypes.Contains(elementType))
                {
                    throw BadImageFormatException(string.Format("Unsupported field or property type when reading a custom attribute signature: {0}", elementType));
                }

                def argumentName = ReadSerString(reader);
                def paramType = TypeSignature(elementType);
                
                def map = createMap(paramType);
                def value = ReadParameterValue(elementType, reader, map);
                
                mutable namedArgument : NamedCustomAttributeArgument = null;                
                when(fieldOrPropByte == FIELD)
                {
                    namedArgument = NamedCustomAttributeFieldArgument(argumentName, value);
                }
                when(fieldOrPropByte == PROPERTY)
                {
                    namedArgument = NamedCustomAttributePropertyArgument(argumentName, value);
                }
                
                when(namedArgument != null)
                {
                    signature.NamedArguments.Add(namedArgument);
                }
            }

            signature;
        }
        private CreateReaderMap([NotNull] reader : BinaryReader, [NotNull] paramType : ITypeSignature, [NotNull] stringHeap : Stream, [NotNull] blobStream : Stream, [NotNull] tablesHeap : ITablesHeap) : Dictionary.[ElementType, void -> object]
        {
            def readerMap : Dictionary.[ElementType, void -> object] = Dictionary();
            readerMap[ElementType.I1] = reader.ReadSByte;
            readerMap[ElementType.I2] = reader.ReadInt16;
            readerMap[ElementType.I4] = reader.ReadInt32;
            readerMap[ElementType.I8] = reader.ReadInt64;
            readerMap[ElementType.U1] = reader.ReadByte;
            readerMap[ElementType.U2] = reader.ReadUInt16;
            readerMap[ElementType.U4] = reader.ReadUInt32;
            readerMap[ElementType.U8] = reader.ReadUInt64;
            readerMap[ElementType.Boolean] = reader.ReadBoolean;
            readerMap[ElementType.Char] = reader.ReadChar;
            readerMap[ElementType.R4] = reader.ReadSingle;    
            readerMap[ElementType.R8] = reader.ReadDouble;
            readerMap[ElementType.String] = () => ReadSerString(reader);
            readerMap[ElementType.Object] = () => ReadBoxedObject(reader, stringHeap, tablesHeap, readerMap);
            readerMap[ElementType.ValueType] = () => ReadValueType(reader, paramType, stringHeap, blobStream, tablesHeap);
            readerMap[ElementType.SzArray] = () => ReadArray(reader, paramType, stringHeap, tablesHeap, readerMap);

            readerMap;
        }
        private ReadParameterValue(elementType : ElementType, [NotNull] reader : BinaryReader, [NotNull] readerMap : IDictionary.[ElementType, void -> object]) : object
        {
            if(elementType == ElementType.Class)
            {
                def typeRef = TypeReference();
                typeRef.QualifiedName = ReadSerString(reader);

                typeRef;
            }
            else
            {
                readerMap[elementType]();
            }
        }                     

        private ReadArray([NotNull] reader : BinaryReader, [NotNull] paramType : ITypeSignature, [NotNull] stringHeap : Stream, [NotNull] tablesHeap : ITablesHeap, 
            [NotNull] readerMap : Dictionary.[ElementType, void -> object]) : object                
        {
            // Read the number of elements in the array
            def arraySignature = paramType :> SzArraySignature;
            def elementCount = reader.ReadUInt32();
            def arrayType = arraySignature.ArrayType;

            def elementType = arrayType.ElementType;                                    

            ReadArray(reader, elementCount, elementType, stringHeap, tablesHeap, readerMap);
        }
        private ReadArray([NotNull] reader : BinaryReader, elementCount : uint, elementType : ElementType, [NotNull] stringHeap : Stream, [NotNull] tablesHeap : ITablesHeap, 
            [NotNull] readerMap : Dictionary.[ElementType, void -> object]) : object
        {
            def typeMap = Dictionary();
            typeMap[ElementType.Boolean] = typeof(bool);
            typeMap[ElementType.Char] = typeof(char);
            typeMap[ElementType.I1] = typeof(sbyte);
            typeMap[ElementType.I2] = typeof(short);
            typeMap[ElementType.I4] = typeof(int);
            typeMap[ElementType.I8] = typeof(long);
            typeMap[ElementType.U1] = typeof(byte);
            typeMap[ElementType.U2] = typeof(ushort);
            typeMap[ElementType.U4] = typeof(uint);
            typeMap[ElementType.U8] = typeof(ulong);
            typeMap[ElementType.R4] = typeof(float);
            typeMap[ElementType.R8] = typeof(double);
            typeMap[ElementType.String] = typeof(string);
            typeMap[ElementType.Object] = typeof(object);

            when(!typeMap.ContainsKey(elementType))
            {
                throw NotSupportedException(string.Format("The element type '{0}' is not supported when reading custom attribute array signatures", elementType));
            }

            // Get the type name for the array type
            def actualType = typeMap[elementType];

            // Create an instance of that array type and populate it with the arguments
            def items = Array.CreateInstance(actualType, elementCount);

            def readElement  = () => readerMap[elementType]();            
            def count = elementCount :> int;
            for(mutable i = 0; i < count; i++)
            {
                def value = readElement();
                items.SetValue(value, i);
            }

            items;
        }        

        private ReadValueType([NotNull] reader : BinaryReader, [NotNull] paramType : ITypeSignature, [NotNull] stringHeap : Stream, [NotNull] blobHeap : Stream, [NotNull] tablesHeap : ITablesHeap) : object
        {
            def parameterType = paramType :> ValueTypeSignature;
            def signature = TypeDefOrRefEncodedSignature();

            signature.Row = parameterType.Row;
            signature.TableId = parameterType.TableId;

            def typeRow = signature.GetTypeRow(tablesHeap);
            def qualifiedName = typeRow.GetPartialQualifiedName(stringHeap, tablesHeap);

            def enumType = Type.GetType(qualifiedName, true);
            def underlyingType = Enum.GetUnderlyingType(enumType);

            def readerMap = Dictionary.[Type, void -> object]();

            readerMap[typeof(byte)] = reader.ReadByte;
            readerMap[typeof(ushort)] = reader.ReadUInt16;
            readerMap[typeof(uint)] = reader.ReadUInt32;
            readerMap[typeof(ulong)] = reader.ReadUInt64;

            when(!readerMap.ContainsKey(underlyingType))
            {
                throw NotSupportedException(string.Format("Underlying type '{0}' not supported when reading enums in custom attribute signatures", underlyingType.Name));
            }

            def enumValue = readerMap[underlyingType]();
            Enum.ToObject(enumType, enumValue);
        }

        private ReadBoxedObject([NotNull] reader : BinaryReader,  [NotNull] stringHeap : Stream, [NotNull] tablesHeap : ITablesHeap, [NotNull] readerMap : Dictionary.[ElementType, void -> object]) : object
        {
            mutable result : object = null;
            def validElements = array[
                                    ElementType.Boolean,
                                    ElementType.Char,
                                    ElementType.I1,
                                    ElementType.I2,
                                    ElementType.I4,
                                    ElementType.I8,
                                    ElementType.U1,
                                    ElementType.U2,
                                    ElementType.U4,
                                    ElementType.U8,
                                    ElementType.R4,
                                    ElementType.R8,
                                    ElementType.String
                                    ];

            // Read the element type
            def elementByte : byte = reader.ReadByte();
            when(elementByte == 0x55)
            {
                // Handle custom enums here
                def enumName = ReadSerString(reader);
                mutable enumType = Type.GetType(enumName, false);

                // If the type load fails, scan the current assembly and
                // its referenced assemblies for a possible match                
                when (enumType == null)
                {

                    def typeDefRows = tablesHeap.GetRows.[TypeDefRow]().Cast.[ITypeRow]();
                    def typeRefRows = tablesHeap.GetRows.[TypeRefRow]().Cast.[ITypeRow]();
                    def typeRows = List();

                    def isTargetType(row : ITypeRow) : bool
                    {
                        row.GetPartialQualifiedName(stringHeap, tablesHeap).StartsWith(enumName);                        
                    }

                    typeRows.AddRange(typeDefRows);
                    typeRows.AddRange(typeRefRows);

                    def targetRow = typeRows.First(isTargetType);
                    def qualifiedName = targetRow.GetPartialQualifiedName(stringHeap, tablesHeap);

                    enumType = Type.GetType(qualifiedName, true);
                }

                def underlyingType = Enum.GetUnderlyingType(enumType);
                def readers = Dictionary.[Type, void -> object]();

                readers[typeof(byte)] = reader.ReadByte;
                readers[typeof(ushort)] = reader.ReadUInt16;
                readers[typeof(uint)] = reader.ReadUInt32;
                readers[typeof(ulong)] = reader.ReadUInt64;

                def enumValue = readers[underlyingType]();
                result = Enum.ToObject(enumType, enumValue);
            }

            when(elementByte == 0x1D)
            {
                // Handle single dimensional arrays
                def arrayElementType = reader.ReadByte() :> ElementType;
                def elementCount = reader.ReadUInt32();

                result = ReadArray(reader, elementCount, arrayElementType, stringHeap, tablesHeap, readerMap);          
            }

            def elementType = elementByte :> ElementType;
            if(validElements.Contains(elementType))
            {
                result = readerMap[elementType]();                
            }
            else
            {
                when(elementByte != 0x55 && elementByte != 0x1D)
                    throw BadImageFormatException(string.Format("Element type '{0}' is not a supported boxed element value for custom attribute signatures", elementType));
            }

            result;
        }
        private ReadSerString([NotNull] reader : BinaryReader) : string
        {
            def underlyingStream = reader.BaseStream;
            def lengthByteValue = underlyingStream.PeekByte();

            mutable result : string = null;
            match(lengthByteValue)
            {
                | 0x00 => result = string.Empty;
                | 0xFF => result = null;
                |_ => 
                    {
                        def integerReader = CompressedIntegerReader();
                        def length = integerReader.ReadInteger(underlyingStream);

                        def bytes = reader.ReadBytes(length :> int);
                        result = Encoding.UTF8.GetString(bytes);
                    }
            }

            // Move the byte position to the next offset if the string value is null or empty
            when(lengthByteValue == 0xFF || lengthByteValue == 0x00)
            {
                _ = underlyingStream.Seek(1, SeekOrigin.Current);
            }
            
            result;
        }
    }
}
