using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

namespace Tao.Metadata.Signatures
{
    public class MethodRefSignatureWriter : ManagedMethodSignatureWriter[IManagedMethodSignature]
    {
        protected override WriteParameters([NotNull] signature : IManagedMethodSignature, [NotNull] stream : Stream) : void
            requires Equals((signature :> MethodRefSignature).ParameterCount, 
            (((signature :> MethodRefSignature).AdditionalParameters.Count + (signature :> MethodRefSignature).Parameters.Count) :> uint))
                otherwise throw ArgumentException("Invalid MethodRefSignature; the total ParameterCount doesn't match the actual number of parameters in the signature")
        {
            def paramWriter = ParamWriter();
            def methodSignature = signature :> MethodRefSignature;
            foreach(currentParam in methodSignature.Parameters)
            {
                paramWriter.Write(currentParam, stream);
            }
            
            // Write the sentinel mark
            stream.WriteByte(MethodSignature.Sentinel);            
            
            // Write the additional parameters
            foreach(currentParam in methodSignature.AdditionalParameters)
            {
                paramWriter.Write(currentParam, stream);
            }
        }
    }
}
