using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

namespace Tao
{
  public abstract class MethodSignatureElementWriter[TElement] 
    where TElement : IMethodSignatureElement, class
  {
      public Write([NotNull] param : TElement, [NotNull] stream : Stream) : void
        requires !object.ReferenceEquals(param.Type, null)
        requires stream.CanWrite
        requires param.Type != null
      {          
          def paramType = param.Type;
          when(!SupportsWriting(param.Type))
          {
              throw NotSupportedException(string.Format("The signature type '{0}' is not supported for write operations", paramType.ElementType));
          }
          
          def elementType = paramType.ElementType;
          def writer = BinaryWriter(stream);
          
          // Write the custom mods
          def modWriter = CustomModWriter();
          foreach(mod in param.CustomMods)
          {
              modWriter.Write(mod, stream);
          }
          
          when(param.IsByRef)
          {
              writer.Write(ElementType.ByRef :> byte);
          }
          
          writer.Write(elementType :> byte);
      }
      
      protected virtual SupportsWriting([NotNull] type : TypeSignature) : bool
      {
          true;
      }
  }
}
