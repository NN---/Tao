using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Tao;
using Tao.Interfaces;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

namespace Tao.Metadata.Tables.Rows
{  
    public class TypeDefRow : IHasCustomAttributeRow, IMemberRefParentRow, ITypeRow
    {
        public Flags : uint { get; set; }
        public TypeName : IIndex { get; set; }
        public TypeNamespace : IIndex { get; set; }
        public Extends : IIndex { get; set; }
        public FieldList : IIndex { get; set; }
        public MethodList : IIndex { get; set; }
        public RowIndex : uint { get; set; }
        public TableId : TableId 
        { 
            get
            {
                TableId.TypeDef;
            }
        }
        public GetAssemblyRow([NotNull] heap : ITablesHeap) : IAssemblyRow
            ensures value != null
        {
            // NOTE: This method assumes that the given heap is the same heap that contains the current TypeDefRow
            def assemblyRow = heap.GetRow.[AssemblyRow](1);          
            assemblyRow;
        }
        public GetPartialQualifiedName([NotNull] stringHeap : Stream, [NotNull] tablesHeap : ITablesHeap) : string
        {
            def typeName = TypeName.ToString(stringHeap);
            def nameSpace = TypeNamespace.ToString(stringHeap);

            def assemblyRow = GetAssemblyRow(tablesHeap);            
            def assemblyName = assemblyRow.Name.ToString(stringHeap);                                                 

            def format = "{0}.{1}, {2}";
            def qualifiedName = string.Format(format, nameSpace, typeName, assemblyName);
            qualifiedName;
        }
        public GetInterfaceImplRows([NotNull] tablesHeap : ITablesHeap) : IEnumerable.[InterfaceImplRow]
        {
            def interfaceRows = tablesHeap.GetRows.[InterfaceImplRow]();
            foreach(row in interfaceRows)
            {
                when(row.Class != null)
                {
                    def typeRowIndex = row.Class.ToUInt32();
                    when(typeRowIndex == RowIndex)
                    {
                        yield row;
                    }
                }
            }
        }
        public GetMethodDefRows([NotNull] tablesHeap : ITablesHeap) : IEnumerable.[MethodDefRow]
            requires RowIndex > 0 otherwise throw InvalidOperationException("The row index for any given type row should never be zero")
        {
            def typeDefRows = tablesHeap.GetRows.[TypeDefRow]();
            def methodDefRows = tablesHeap.GetRows.[MethodDefRow]();

            def typeRows = typeDefRows.ToArray();
            def methodRows = methodDefRows.ToArray();

            def typeRowCount = typeRows.Length;
            def currentIndex = (RowIndex - 1) :> int;
            def lastIndex = typeRowCount - 1;

            def startingMethodRowIndex = MethodList.ToUInt32();            
            mutable lastMethodRowIndex = startingMethodRowIndex;
            when(currentIndex < lastIndex)
            {
                mutable nextTypeRow : TypeDefRow = typeRows[currentIndex + 1];
                lastMethodRowIndex = nextTypeRow.MethodList.ToUInt32();
            }

            def results = List.[MethodDefRow]();
            def rowsToExtract = lastMethodRowIndex - startingMethodRowIndex;
            def methodRowCount = methodRows.Length :> uint;
            when(startingMethodRowIndex <= methodRowCount && rowsToExtract > 0)
            {
                for(mutable i = startingMethodRowIndex; i < lastMethodRowIndex; i++)
                {
                    // Shift the index by one since table pointers start at index 1
                    def actualIndex = (i -1) :> int;
                    def currentRow = methodRows[actualIndex];
                    results.Add(currentRow);
                }
            }

            results;
        }
    }
}
