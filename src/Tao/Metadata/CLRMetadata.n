using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using Tao.Interfaces;
using Tao.Metadata.Tables;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

namespace Tao.Metadata
{
    public class CLRMetadata : ICLRMetadata
    {
        private _root : IMetadataRoot;
        private _tablesHeap : ITablesHeap;
        public this(root : IMetadataRoot)
            requires root != null && root.Heaps.ContainsKey("#~")
        {
            _root = root;
                       
            def tableStream = root.Heaps["#~"];
            
            _tablesHeap = TablesHeap();
            _tablesHeap.ReadFrom(tableStream);            
        }
        
        public ReadBlob(index : uint) : Stream
            requires _root.Heaps != null && _root.Heaps.ContainsKey("#Blob")
        {
            def blobStream = _root.Heaps["#Blob"];
            def reader = BlobReader();
            reader.Read(index, blobStream);            
        }
        public ReadGuid(index : uint) : Guid
            requires _root.Heaps != null && _root.Heaps.ContainsKey("#GUID")
        {
            def guidHeap = _root.Heaps["#GUID"];
            def reader = GuidReader();
            reader.Read(index, guidHeap);
        }
        public ReadString(index : uint) : string
            requires _root.Heaps != null && _root.Heaps.ContainsKey("#Strings")
        {
            def stringHeap = _root.Heaps["#Strings"];
            def reader = StringHeapReader();
            reader.Read(index, stringHeap);            
        }
        public ReadUserString(index : uint) : string
            requires _root.Heaps != null && _root.Heaps.ContainsKey("#US")
        {
            def userStringHeap = _root.Heaps["#US"];
            def reader = UserStringHeapReader();
            reader.Read(index, userStringHeap);
        }
        
        public Tables : IEnumerable.[IMetadataTable]
        {
            get
            {
                _tablesHeap.Tables;    
            }
        }
    }
}
