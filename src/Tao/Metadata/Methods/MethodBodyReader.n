using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

using Tao;
using Tao.Interfaces;
using Tao.Metadata.Tables;
using Tao.Metadata.Tables.Rows;
using Tao.Metadata.Instructions;
using Tao.Metadata.Instructions.Operands;

namespace Tao.Metadata
{  
    public class MethodBodyReader : IMethodBodyReader
    {
        private _reader : IMethodBodyReader;
        private _instructionReader : IInstructionReader;
        
        public this([NotNull] root : IMetadataRoot)
        {
            def userStringHeap = root.GetUserStringHeap();
            def boundReader = BoundStringReader(UserStringHeapReader(), userStringHeap);
            this(boundReader);
        }
        
        public this([NotNull] stringReader : IBoundStringReader)
        {
            this(IndividualInstructionReader(stringReader));
        }
        
        public this([NotNull] individualInstructionReader : IIndividualInstructionReader)
        {
            this(SimpleMethodBodyReader(), InstructionReader(individualInstructionReader));
        }
        
        public this([NotNull] reader : IMethodBodyReader, [NotNull] instructionReader : IInstructionReader)
        {
            _reader = reader;
            _instructionReader = instructionReader;
        }
        
        public Read([NotNull] methodStream : Stream) : IMethodBody
            requires methodStream.CanRead
            requires _instructionReader != null
            ensures value != null
        {
            def body = _reader.Read(methodStream);
            
            // Convert the method bytes into instructions
            def bytes = body.Bytes;
            def instructionStream = MemoryStream(bytes);
            def instructions = _instructionReader.Read(instructionStream);
            
            foreach(instruction in instructions)
            {
                body.Instructions.Add(instruction);
            }
            
            body;
        }
    }
}
