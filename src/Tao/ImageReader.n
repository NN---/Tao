using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

namespace Tao
{  
  public class ImageReader
  {
    public Read([NotNull] imageStream : Stream) : Image
        requires imageStream.Length > 0 otherwise throw ArgumentException("imageStream", "Unable to read from an empty image stream")
        ensures value.DOSHeader != null
        ensures value.CoffHeader != null        
    {
      def image = Image();
          
      def dosHeaderReader = DosHeaderReader();
      def dosStream = dosHeaderReader.Read(imageStream);
      image.DOSHeader = dosStream;
          
      def dosStreamReader = BinaryReader(dosStream);
      _ = dosStream.Seek(0x3C, SeekOrigin.Begin);
      
      // Determine the location of the COFF header
      def coffHeaderOffset = dosStreamReader.ReadUInt32();
      _ = imageStream.Seek(coffHeaderOffset, SeekOrigin.Begin);
      
      def coffHeaderReader = CoffHeaderReader();
      def coffHeader = coffHeaderReader.Read(imageStream);
      image.CoffHeader = coffHeader;
      
      // Read the fields that are common to both 32 and 64-bit optional headers
      def standardFieldsReader = OptionalHeaderStandardFieldsReader();
      def standardFields = standardFieldsReader.Read(imageStream);
      
      mutable numberOfDirectories : uint = 0;
      match(standardFields.ImageType)
      {
        |ImageType.PE32 =>
        {
            def reader = NTHeader32Reader();
            def header = reader.Read(imageStream);
            image.OptionalHeader = OptionalHeader.OptionalHeader32(standardFields, header);
            
            numberOfDirectories = header.NumberOfRvaAndSizes;
        };                                   
        |ImageType.PE32Plus =>
        {
            def reader = NTHeader64Reader();
            def header = reader.Read(imageStream);
            image.OptionalHeader = OptionalHeader.OptionalHeader64(standardFields, header);
            
            numberOfDirectories = header.NumberOfRvaAndSizes;
        };
      }
      
      // Read the data directories
      def directoryReader = DataDirectoryReader();
      def directories = directoryReader.Read(numberOfDirectories :> ushort, imageStream);
      foreach(directory in directories)
      {
          image.DataDirectories.Add(directory);
      }
      
      // Read the section headers and their corresponding
      // section data
      def sectionHeaderReader = SectionHeaderReader();
      def numberOfSections = coffHeader.NumberOfSections;
      def sectionHeaders = sectionHeaderReader.Read(numberOfSections, imageStream);
      
      // Keep track of the original position after the section header read operation
      def originalPosition = imageStream.Position;
      foreach(header in sectionHeaders)
      {
          def section = Section();
          section.Header = header;
          
          def pointerToRawData = header.PointerToRawData :> int;
          def size = header.SizeOfRawData :> int ;
          
          // Seek the start position of the section itself
          _ = imageStream.Seek(pointerToRawData, SeekOrigin.Begin);
          
          def buffer = array(size) : array[byte];
          def reader = BinaryReader(imageStream);
          _ = reader.Read(buffer, 0, size);
          
          section.Data = MemoryStream(buffer);
          
          image.Sections.Add(section);
      }
      
      // Restore the original stream position after the stream header read
      imageStream.Position = originalPosition;
      
      image;
    }
  }
}
